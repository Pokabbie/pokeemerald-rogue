
const FLAG_HIDE_BANK = FLAG_TEMP_1
const FLAG_HIDE_TREATS = FLAG_TEMP_2
const FLAG_HIDE_POKEBLOCK = FLAG_TEMP_3

mapscripts Rogue_Area_Market_MapScripts 
{
    MAP_SCRIPT_ON_LOAD: Rogue_Area_Market_OnMapLoad
}


script Rogue_Area_Market_OnMapLoad
{
    hubupgradeunlocked(HUB_UPGRADE_MARKET_BANK)
    if(var(VAR_RESULT) == YES)
    {
        clearflag(FLAG_HIDE_BANK)
    }
    else
    {
        setflag(FLAG_HIDE_BANK)
    }

    hubupgradeunlocked(HUB_UPGRADE_MARKET_TREAT_SHOP)
    if(var(VAR_RESULT) == TRUE)
    {
        clearflag(FLAG_HIDE_TREATS)
    }
    else
    {
        setflag(FLAG_HIDE_TREATS)
    }

    hubupgradeunlocked(HUB_UPGRADE_MARKET_POKEBLOCK_BLENDERS)
    if(var(VAR_RESULT) == TRUE)
    {
        clearflag(FLAG_HIDE_POKEBLOCK)
    }
    else
    {
        setflag(FLAG_HIDE_POKEBLOCK)
    }
}



script Rogue_Area_Market_ShopBank
{
    lock
    faceplayer
    message(format("Welcome to the Bank!\pI can provide you with Coin Cases which will convert into instantaneous Pok√©dollars whenever you embark on an Adventure.\pWould you like to buy and Coin Cases?"))
    waitmessage

    pokemart(BankMart)

    msgbox("Come again soon!")
    release
}

script Rogue_Area_Market_BankSign
{
    hubupgradeunlocked(HUB_UPGRADE_MARKET_BANK)
    if(var(VAR_RESULT) == YES)
    {
        msgbox(format("The Bank"), MSGBOX_SIGN)
    }
}

mart BankMart
{
    ITEM_SMALL_COIN_CASE
    ITEM_LARGE_COIN_CASE
}


script Rogue_Area_Market_BlendPokeblockNovice
{
    setflag(FLAG_HIDE_LILYCOVE_CONTEST_HALL_BLEND_MASTER)
    
    lock

    // NOVICE_BERRY_COUNT
    msgbox(format("Hello!\nI'm a {POKEBLOCK} Blending Novice.\pI can help you produce up to 10 {POKEBLOCK} per 20 berries.\pWould you like to blend some {POKEBLOCK} with me?"), MSGBOX_YESNO)

    if(var(VAR_RESULT) == YES)
    {
        specialvar(VAR_RESULT, PlayerHasBerries)

        if(var(VAR_RESULT) == YES)
        {
            // NOVICE_BERRY_COUNT
            setvar(VAR_0x800A, 20) // num berries

            setvar(VAR_RESULT, YES)
            while(var(VAR_RESULT) == YES)
            {
                call(Rogue_Area_Market_BlendPokeblockNoviceLoop)
            }
        }
        else
        {
            msgbox(format("Oh.\pIt doesn't look like you have any berries to blend."))
        }
    }
    else
    {
        msgbox(format("I see.\nIf you change your mind feel free to speak with me again."))
    }

    release
}

script Rogue_Area_Market_BlendPokeblockNoviceLoop
{
    msgbox(format("Which berry would you like to blend?"))

    fadescreen(FADE_TO_BLACK)
    special(Bag_ChooseBerry)
    waitstate

    if(var(VAR_ITEM_ID) != 0)
    {
        special(Rogue_IsValidPokeblockBerry)
        if(var(VAR_RESULT) == YES)
        {
            // NOVICE_BERRY_COUNT
            checkitem(VAR_ITEM_ID, 20)

            if(var(VAR_RESULT) == YES)
            {
                msgbox(format("OK.\pLet's blend!"))

                waitse
                setvar(VAR_0x8004, 1) // NUM_OPPONENTS
                fadescreen(FADE_TO_BLACK)
                special(DoBerryBlending)
                waitstate

                call(Rogue_Common_DisplayPopups)

                msgbox(format("Would you like to blend more berries?"), MSGBOX_YESNO)
            }
            else
            {
                // NOVICE_BERRY_COUNT
                msgbox(format("You don't have enough berries.\pYou need 20 berries to blend with me."))
            }
        }
        else
        {
            msgbox(format("I'm afraid that berry won't produce any {POKEBLOCK}.\pYou can use the Check Tag option in your Bag to see which {POKEBLOCK} a berry will produce when blended."))
        }
    }
    else
    {
        setvar(VAR_RESULT, NO)
    }
}

script Rogue_Area_Market_BlendPokeblockMaster
{
    clearflag(FLAG_HIDE_LILYCOVE_CONTEST_HALL_BLEND_MASTER)
    
    lock

    // MASTER_BERRY_COUNT
    msgbox(format("Hello!\nI'm a {POKEBLOCK} Blending Master.\pI can help you produce up to 30 {POKEBLOCK} per 70 berries.\pWould you like to blend some {POKEBLOCK} with me?"), MSGBOX_YESNO)

    if(var(VAR_RESULT) == YES)
    {
        specialvar(VAR_RESULT, PlayerHasBerries)

        if(var(VAR_RESULT) == YES)
        {
            // MASTER_BERRY_COUNT
            setvar(VAR_0x800A, 70) // num berries

            setvar(VAR_RESULT, YES)
            while(var(VAR_RESULT) == YES)
            {
                call(Rogue_Area_Market_BlendPokeblockMasterLoop)
            }
        }
        else
        {
            msgbox(format("Oh.\pIt doesn't look like you have any berries to blend."))
        }
    }
    else
    {
        msgbox(format("I see.\nIf you change your mind feel free to speak with me again."))
    }

    release
}

script Rogue_Area_Market_BlendPokeblockMasterLoop
{
    msgbox(format("Which berry would you like to blend?"))

    fadescreen(FADE_TO_BLACK)
    special(Bag_ChooseBerry)
    waitstate

    if(var(VAR_ITEM_ID) != 0)
    {
        special(Rogue_IsValidPokeblockBerry)
        if(var(VAR_RESULT) == YES)
        {
            // MASTER_BERRY_COUNT
            checkitem(VAR_ITEM_ID, 70)

            if(var(VAR_RESULT) == YES)
            {
                msgbox(format("OK.\pLet's blend!"))

                waitse
                setvar(VAR_0x8004, 1) // NUM_OPPONENTS
                fadescreen(FADE_TO_BLACK)
                special(DoBerryBlending)
                waitstate

                call(Rogue_Common_DisplayPopups)

                msgbox(format("Would you like to blend more berries?"), MSGBOX_YESNO)
            }
            else
            {
                msgbox(format("You don't have enough berries.\pYou need 70 berries to blend with me."))
            }
        }
        else
        {
            msgbox(format("I'm afraid that berry won't produce any {POKEBLOCK}.\pYou can use the Check Tag option in your Bag to see which {POKEBLOCK} a berry will produce when blended."))
        }
    }
    else
    {
        setvar(VAR_RESULT, NO)
    }
}

script Rogue_Area_Market_TalkBerryBlender
{
    lock
    msgbox(format("They're intensely focusing on using the Berry Blending Machine."))
    release
}

script Rogue_Area_Market_TalkPokeblockHelper
{
    lock
    faceplayer
    msgbox(format("You look like a Trainer to me!\pI'm something of a {POKEBLOCK} Expert myself.\pI can help you learn which Berries make what kinds of {POKEBLOCK}, if you'd like."))

    setvar(VAR_RESULT, YES)

    while(var(VAR_RESULT) == YES)
    {
        begindynamicmultichoice(32)
        special(Rogue_AppendMultichoicePokeblockItems)
        message(format("Which type of {POKEBLOCK} are you interested in?"))
        waitmessage
        enddynamicmultichoice(19, 2, 5, TRUE)

        if(var(VAR_RESULT) != MULTI_B_PRESSED)
        {
            copyvar(VAR_0x8004, VAR_RESULT)
            addvar(VAR_0x8004, FIRST_ITEM_POKEBLOCK)
            bufferitemname(STR_VAR_1, VAR_0x8004)

            begindynamicmultichoice(32)
            special(Rogue_AppendMultichoiceBerriesForPokeblock)
            message(format("These are the Berries which produce {STR_VAR_1}."))
            waitmessage
            enddynamicmultichoice(19, 2, 5, TRUE)
            
            setvar(VAR_RESULT, YES)
        }
        else
        {
            setvar(VAR_RESULT, NO)
        }
    }

    release
}