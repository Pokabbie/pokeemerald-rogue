// Stolen state control from this var entirely
const VAR_ROGUE_INTRO_STATE = VAR_LITTLEROOT_INTRO_STATE
const VAR_RETURN_STATE = VAR_TEMP_0

const VAR_TL_X = VAR_0x8000
const VAR_TL_Y = VAR_0x8001
const VAR_BR_X = VAR_0x8002
const VAR_BR_Y = VAR_0x8003

const METATILE_Grass = 0x001

// Mountain/Hole
const METATILE_DirtCentre = 0x10C

const METATILE_DirtLeft = 0x075
const METATILE_DirtRight = 0x073
const METATILE_DirtBottom = 0x002
const METATILE_DirtTop = 0x07C

const METATILE_DirtTopLeft = 0x074
const METATILE_DirtTopRight = 0x089

// Normal Dirt
//const METATILE_DirtCentre = 0x121

//const METATILE_DirtLeft = 0x120
//const METATILE_DirtRight = 0x122
//const METATILE_DirtBottom = 0x129
//const METATILE_DirtTop = 0x119
//
//const METATILE_DirtBottomLeft = 0x128
//const METATILE_DirtBottomRight = 0x12A
//const METATILE_DirtTopLeft = 0x118
//const METATILE_DirtTopRight = 0x11A

const NPC_INTRO_SCOTT = 2
const NPC_POKABBIE = 15

mapscripts Rogue_Hub_MapScripts
{
    MAP_SCRIPT_ON_TRANSITION: RogueHub_OnTransition
    MAP_SCRIPT_ON_LOAD: RogueHub_OnMapLoad
    MAP_SCRIPT_ON_FRAME_TABLE 
    [
        VAR_RETURN_STATE, 0: RogueHub_Intro0
        VAR_RETURN_STATE, 1: RogueHub_EnterPostTutortial
    ]
}

script RogueHub_OnTransition
{
    if(var(VAR_ROGUE_INTRO_STATE) <= 2)
    {
        setobjectxyperm(NPC_INTRO_SCOTT, 23, 6)
    }

    // Show/Hide Pokabbie
    if(flag(FLAG_IS_CHAMPION) && !flag(FLAG_ROGUE_UNCOVERRED_POKABBIE))
    {
        clearflag(FLAG_TEMP_8)
    }
    else
    {
        setflag(FLAG_TEMP_8)
    }
}

script RogueHub_OnMapLoad
{
    // Turn off buildings that haven't unlocked yet

    if(flag(FLAG_ROGUE_DEBUG_DISABLED))
    {
        fillmetatile(4, 20, 4, 20, METATILE_Grass, FALSE)
    }

    // Shop
    questcollected(QUEST_FirstAdventure)
    if(var(VAR_RESULT) == NO)
    {
        fillmetatile(31, 10, 40, 16, METATILE_Grass, FALSE)
        setflag(FLAG_ROGUE_TRAINER0)

        // +clothes shop
        fillmetatile(41, 20, 48, 25, METATILE_Grass, FALSE)
    }
    else
    {
        clearflag(FLAG_ROGUE_TRAINER0)
    }

    // Safari Zone
    questcollected(QUEST_Collector1)
    if(var(VAR_RESULT) == NO)
    {
        fillmetatile(1, 3, 14, 10, METATILE_Grass, FALSE)
        fillmetatile(1, 11, 12, 11, METATILE_Grass, FALSE)
        fillmetatile(1, 12, 6, 12, METATILE_Grass, FALSE)

        // +name rater
        fillmetatile(31, 21, 37, 26, METATILE_Grass, FALSE)
        setflag(FLAG_ROGUE_TRAINER4)
    }
    else
    {
        clearflag(FLAG_ROGUE_TRAINER4)
    }

    // Bike Shop
    questcollected(QUEST_Bike1)
    if(var(VAR_RESULT) == NO)
    {
        fillmetatile(22, 12, 27, 16, METATILE_Grass, FALSE)
        setflag(FLAG_ROGUE_TRAINER1)
    }
    else
    {
        clearflag(FLAG_ROGUE_TRAINER1)
    }

    // Tutor area
    questcollected(QUEST_NoFainting1)
    if(var(VAR_RESULT) == NO)
    {
        fillmetatile(42, 14, 48, 19, METATILE_Grass, FALSE)
        setflag(FLAG_ROGUE_TRAINER2)
    }
    else
    {
        clearflag(FLAG_ROGUE_TRAINER2)
    }

    // Travelling Shop
    questcollected(QUEST_ShoppingSpree)
    if(var(VAR_RESULT) == NO)
    {
        fillmetatile(22, 8, 24, 8, METATILE_Grass, FALSE)
    
        setflag(FLAG_ROGUE_TRAINER7)
        setflag(FLAG_ROGUE_TRAINER5)
    }
    else
    {
        clearflag(FLAG_ROGUE_TRAINER7)
        clearflag(FLAG_ROGUE_TRAINER5)

        if(flag(FLAG_ROGUE_RARE_ITEM_MART_DISABLED))
        {
            setflag(FLAG_ROGUE_TRAINER5)
        }
    }

    // Bank
    questcollected(QUEST_BigSaver)
    if(var(VAR_RESULT) == NO)
    {
        fillmetatile(13, 27, 27, 36, METATILE_Grass, FALSE)
        setflag(FLAG_ROGUE_TRAINER3)
    }
    else
    {
        clearflag(FLAG_ROGUE_TRAINER3)
    }

    // Berry Patch
    questcollected(QUEST_BerryCollector)
    if(var(VAR_RESULT) == NO)
    {
        fillmetatile(10, 40, 16, 41, METATILE_Grass, FALSE)
        fillmetatile(18, 40, 24, 41, METATILE_Grass, FALSE)
        fillmetatile(26, 40, 32, 41, METATILE_Grass, FALSE)
        fillmetatile(34, 40, 40, 41, METATILE_Grass, FALSE)
        setflag(FLAG_ROGUE_TRAINER8)
    }
    else
    {
        clearflag(FLAG_ROGUE_TRAINER8)
    }

    // Bakery
    questcollected(QUEST_BerryCollector)
    if(var(VAR_RESULT) == NO)
    {
        fillmetatile(31, 31, 37, 36, METATILE_Grass, FALSE)
    }
    else
    {
    }

    // Intro Scott
    if(var(VAR_ROGUE_INTRO_STATE) > 3)
    {
        setflag(FLAG_ROGUE_TRAINER6)
    }
}

script RogueHub_Intro0
{
    if(var(VAR_ROGUE_INTRO_STATE) == 0)
    {
        lockall
        //delay(15)
        playse(SE_LEDGE)
        applymovement(OBJ_EVENT_ID_PLAYER, RogueHub_Intro_StepOffTruck)
        waitmovement(0)

        applymovement(NPC_INTRO_SCOTT, RogueHub_Intro_ScottGreetAtTruck)
        waitmovement(0)

        applymovement(OBJ_EVENT_ID_PLAYER, RogueHub_Intro_PlayerGreetAtTruck)
        waitmovement(0)

        msgbox(format("SCOTT: Well we're here!\pThe POKéMON HUB!\pSorry again for making you sit in the back with all the supplies.\pI'm sure you're raring to go, but first you should go speak the PROF."))

        applymovement(NPC_INTRO_SCOTT, RogueHub_Intro_ScottWalkToPost)
        waitmovement(0)

        // Have to rest coords here otherwise will be stood in wrong place when walk off screen
        setobjectxyperm(NPC_INTRO_SCOTT, 29, 6)

        setvar(VAR_ROGUE_INTRO_STATE, 3)
        setflag(FLAG_HIDE_LITTLEROOT_TOWN_BRENDANS_HOUSE_TRUCK)
        releaseall
    }

    setvar(VAR_RETURN_STATE, 1)
}

movement RogueHub_Intro_StepOffTruck 
{
    jump_right
    delay_16 * 2
}

movement RogueHub_Intro_PlayerGreetAtTruck 
{
    face_up
}

movement RogueHub_Intro_ScottGreetAtTruck
{
    walk_left
    walk_up
    walk_right * 3
    walk_down
}

movement RogueHub_Intro_ScottWalkToPost
{
    walk_right * 4
    face_down
}

script RogueHub_EnterPostTutortial
{
    call(RogueHub_IntroPokabbie)
    setvar(VAR_RETURN_STATE, 2)
}

script RogueHub_SpeakScott
{
    lock
    faceplayer

    if(var(VAR_ROGUE_INTRO_STATE) <= 3)
    {
        msgbox(format("SCOTT: You should go and speak to find the PROF. first"), MSGBOX_DEFAULT)
    }
    else
    {
        msgbox(format("SCOTT: Uh oh!\pI'm not suppose to be standing here...\pYou probably loaded this SAVE from an old game version.\pTry entering a house and checking if I'm still here."), MSGBOX_DEFAULT)
    }

    release
    end
}

script RogueHub_IntroPokabbie
{
    // Show/Hide Pokabbie
    if(flag(FLAG_IS_CHAMPION) && !flag(FLAG_ROGUE_UNCOVERRED_POKABBIE))
    {
        lockall
        showobjectat(NPC_POKABBIE, MAP_ROGUE_HUB)

        msgbox(format("{PLAYER}!"), MSGBOX_NPC)
        turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)

        applymovement(NPC_POKABBIE, RogueHub_Pokabbie_Enter)
        waitmovement(0)

        msgbox(format("???: Congratulations {PLAYER}!\nI can't believe you managed to defeat the CHAMPION!\pOh of course.\nWhere I my manners!\nAllow me to introduce myself!"), MSGBOX_NPC)
    
        applymovement(NPC_POKABBIE, RogueHub_Pokabbie_Spin)
        waitmovement(0)

        msgbox(format("The name's Abbie!\nBut you can call me Pokabbie!"), MSGBOX_NPC)
        msgbox(format("POKABBIE: I'm the DEV behind EMERALD ROGUE!\pAs a reward for beating the CHAMPION I would like to welcome you to my LAB!\pI've uncovered the secret entrance inside the CONFIG LAB, so feel free to come take a look at some point."), MSGBOX_NPC)
    
        applymovement(NPC_POKABBIE, RogueHub_Pokabbie_Spin)
        waitmovement(0)
        
        applymovement(NPC_POKABBIE, RogueHub_Pokabbie_Exit)
        waitmovement(0)

        setflag(FLAG_ROGUE_UNCOVERRED_POKABBIE)
        setflag(FLAG_TEMP_8)
        removeobject(NPC_POKABBIE)

        releaseall
    }
}

movement RogueHub_Pokabbie_Enter 
{
    walk_fast_up * 5
}

movement RogueHub_Pokabbie_Spin 
{
    face_right
    delay_1

    face_up
    delay_1

    face_left
    delay_1

    face_down
    delay_1
    
    face_right
    delay_1

    face_up
    delay_1

    face_left
    delay_1

    face_down
    delay_1

    face_up
}

movement RogueHub_Pokabbie_Exit
{
    walk_fast_down * 5
}

script RogueHub_SpeakBikeShop
{
    lock
    faceplayer

    checkitem(ITEM_ACRO_BIKE)
    copyvar(VAR_0x8000, VAR_RESULT)

    checkitem(ITEM_MACH_BIKE)
    copyvar(VAR_0x8001, VAR_RESULT)

    questcollected(QUEST_Bike2)
    if(var(VAR_RESULT) == YES)
    {
        if(var(VAR_0x8000) == NO)
        {
            bufferitemname(STR_VAR_1, ITEM_ACRO_BIKE)
            msgbox(format("Whoa {PLAYER}!\pYou're looking much faster now, so here take the {STR_VAR_1}!"), MSGBOX_DEFAULT)
            giveitem(ITEM_ACRO_BIKE)
        }
        elif(var(VAR_0x8001) == NO)
        {
            bufferitemname(STR_VAR_1, ITEM_MACH_BIKE)
            msgbox(format("Whoa {PLAYER}!\pYou're looking much faster now, so here take the {STR_VAR_1}!"), MSGBOX_DEFAULT)
            giveitem(ITEM_MACH_BIKE)
        }
        else
        {
            msgbox(format("Hey {PLAYER}!\pKeep up the quick pace!"), MSGBOX_DEFAULT)
        }
    }
    else
    {
        if(var(VAR_0x8000) == YES) // Has ACRO bike
        {
            bufferitemname(STR_VAR_1, ITEM_MACH_BIKE)
            msgbox(format("Do you want to swap to the {STR_VAR_1}?"), MSGBOX_YESNO)

            if(var(VAR_RESULT) == YES)
            {
                removeitem(ITEM_ACRO_BIKE)
                giveitem(ITEM_MACH_BIKE)
            }
        }
        elif(var(VAR_0x8001) == YES) // Has MACH bike
        {
            bufferitemname(STR_VAR_1, ITEM_ACRO_BIKE)
            msgbox(format("Do you want to swap to the {STR_VAR_1}?"), MSGBOX_YESNO)

            if(var(VAR_RESULT) == YES)
            {
                removeitem(ITEM_MACH_BIKE)
                giveitem(ITEM_ACRO_BIKE)
            }
        }
        else
        {
            msgbox(format("Finally!\pYou must be this {PLAYER} I've been told about.\pYou seem a bit slower than I expected...\pI know!\nI have a couple old BIKEs I've been looking to get rid of.\pYou can have them!"))

            giveitem(ITEM_ACRO_BIKE)

            msgbox(format("On second thought...\nYou look like you're struggling to carry that much.\pTell you what. You take that one and if you ever want to swap it out for the other one, let me know"))
        }
    }

    release
}
script RogueHub_SpeakSafariWelcome
{
    lock
    faceplayer

    msgbox(format("Welcome to the SAFARI ZONE!\pWe use the data from your POKéDEX to breed POKéMON and let them roam freely!\pAs a thanks for providing the POKéMON data for us, we'll allow you to enter and capture a few POKéMON of your choosing which you may then use as a PARTNER in future adventures!"), MSGBOX_NPC)

    release
}


script RogueHub_SpeakFisherman
{
    lock
    faceplayer

    checkitem(ITEM_SUPER_ROD)
    if(var(VAR_RESULT) == YES)
    {
        msgbox(format("Hope that SUPER ROD is doing good work for you!"), MSGBOX_DEFAULT)
    }
    else
    {
        msgbox(format("Oh you're that TRAINER I keep hearing about!\pYou know, if you find yourself short on WATER POKéMON you can always use a FISHING ROD\pWhat!?\nYou don't have one!\pHere, you should take this one!"), MSGBOX_DEFAULT)
        giveitem(ITEM_SUPER_ROD)
    }

    release
}

script RogueHub_SpeakBank
{
    lock
    faceplayer

    if(var(VAR_ROGUE_ADVENTURE_MONEY) == 0)
    {
        // Tutorial
        msgbox(format("Why hello!\nYou must be {PLAYER} if I'm not mistaken?\pWonderful!\nYou see I'm rather useful when it comes to managing money and I was hoping to setup an ADVENTURE FUND for yourself."), MSGBOX_DEFAULT)
        msgbox(format("What's an ADVENTURE FUND?\nIn essence, if you donate your HUB MONEY to myself, I will invest it on your behalf and then every time you head out on another adventure, I will give you some extra money to start with.\pSo rather than having to start with ¥0, by donating money to myself, you can start with ¥1000, ¥2000, ¥10000!\pThe sky is the limit really!\nOr rather I guess the limit would be your HUB MONEY."), MSGBOX_DEFAULT)
    
        msgbox(format("Here, let me get you started with a gift.\pI shall add ¥500 to your ADVENTURE FUND out of my own pocket!"), MSGBOX_DEFAULT)
        setvar(VAR_ROGUE_ADVENTURE_MONEY, 500)
        playse(SE_SHOP)
        waitse
    }

    showmoneybox(0, 0)

    buffernumberstring(STR_VAR_2, VAR_ROGUE_ADVENTURE_MONEY)
    if(var(VAR_ROGUE_ADVENTURE_MONEY) >= 25000)
    {
        msgbox(format("Your fund currently has ¥{STR_VAR_2}.\pI'm afraid you can't donate any more money at the moment."), MSGBOX_DEFAULT)
    }
    else
    {
        msgbox(format("Your fund currently has ¥{STR_VAR_2}.\pWould you like to deposit another ¥500?"), MSGBOX_YESNO)
        if(var(VAR_RESULT) == YES)
        {
            checkmoney(500)
            if(var(VAR_RESULT) == NO)
            {
                msgbox(format("I'm afraid you seem to be lacking the required funds currently."), MSGBOX_YESNO)
            }
            else
            {
                removemoney(500)
                updatemoneybox

                addvar(VAR_ROGUE_ADVENTURE_MONEY, 500)
                buffernumberstring(STR_VAR_2, VAR_ROGUE_ADVENTURE_MONEY)

                playse(SE_SHOP)
                waitse
                
                msgbox(format("Thankyou very much!\nYour fund now contains ¥{STR_VAR_2}."), MSGBOX_DEFAULT)
            }
        }
    }
    hidemoneybox

    if(var(VAR_RESULT) == NO)
    {
        msgbox(format("I see...\nYou know where I am, if you change your mind."), MSGBOX_DEFAULT)
    }
    release
}

script RogueHub_SpeakBerryMerchant
{
    lock
    faceplayer

    if(flag(FLAG_ROGUE_MET_BERRY_MERCHANT) == FALSE)
    {
        msgbox(format("Oh hello {PLAYER}!\nI hope you don't mind, but I've setup some berry patches.\pYou may be given berries are rewards for completing quest that you're welcome to plant in this field!"))

        //msgbox(format("Here's a little care package to get you started"))
        //giveitem(ITEM_ORAN_BERRY, 1)
        //giveitem(ITEM_PECHA_BERRY, 1)
        //giveitem(ITEM_RAWST_BERRY, 1)
        
        msgbox(format("If you ever run out of berries or if you'd like to buy some rather than grow them, then please do come and speak with me."))

        setflag(FLAG_ROGUE_MET_BERRY_MERCHANT)
    }
    else
    {
        message("Hi {PLAYER}!\nWould you like to buy some berries?")
        waitmessage

        rogue_dynamicpokemart(ROGUE_SHOP_BERRIES)

        msgbox("Glad to be of help!")
    }

    release
}

script RogueHub_Sign_AdventureSign
{
    buffernumberstring(STR_VAR_1, VAR_ROGUE_FURTHEST_DIFFICULTY)
    msgbox(format("Adventures await beyond this point..."), MSGBOX_SIGN)
}

script RogueHub_Sign_ProfLab
{
    msgbox(format("PROF. BIRCH's LAB"), MSGBOX_SIGN)
}

script RogueHub_Sign_ConfigLab
{
    msgbox(format("CONFIG LAB"), MSGBOX_SIGN)
}

script RogueHub_Sign_Safari
{
    msgbox(format("SAFARI ZONE"), MSGBOX_SIGN)
}

script RogueHub_Sign_Shop
{
    msgbox(format("OUTDOOR MART"), MSGBOX_SIGN)
}

script RogueHub_Sign_BikeShop
{
    msgbox(format("BIKE SHOP"), MSGBOX_SIGN)
}

script RogueHub_Sign_NameRater
{
    msgbox(format("NAME RATER's HOUSE"), MSGBOX_SIGN)
}

script RogueHub_Sign_Bank
{
    msgbox(format("BANK"), MSGBOX_SIGN)
}

script RogueHub_Sign_ClothesShop
{
    msgbox(format("CLOTHES SHOP"), MSGBOX_SIGN)
}

script RogueHub_Sign_Bakery
{
    msgbox(format("BAKERY"), MSGBOX_SIGN)
}

script RogueHub_EnterSafari
{
    lockall
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterUp)
    waitmovement(0)

    showmoneybox(0, 0)
    msgbox(format("Welcome to the SAFARI ZONE!\nWould like to enter?"), MSGBOX_YESNO)

    setvar(VAR_0x8000, 0)
    setvar(VAR_0x8000, 0)

    if(var(VAR_RESULT) == YES)
    {
        msgbox(format("Would you like to limit which GENERATION of POKéMON will be wandering about for you to catch?"), MSGBOX_YESNO)

        if(var(VAR_RESULT) == YES)
        {
            message(format("Which GENERATION would you like to enable?"))
            waitmessage
            
            if(flag(FLAG_ROGUE_EXPANSION_ACTIVE) && flag(FLAG_ROGUE_MET_POKABBIE))
            {
                multichoice(20, 0, MULTI_GENERATIONS_UPTO8, NO)
            }
            else
            {
                multichoice(20, 6, MULTI_GENERATIONS_UPTO3, NO)
            }

            switch(var(VAR_RESULT))
            {
                case MULTI_B_PRESSED: 
                    setvar(VAR_ROGUE_SAFARI_GENERATION, 0)
                
                default:
                    copyvar(VAR_ROGUE_SAFARI_GENERATION, VAR_RESULT)
                    addvar(VAR_ROGUE_SAFARI_GENERATION, 1)
            }
        }
        else
        {
            setvar(VAR_ROGUE_SAFARI_GENERATION, 0)
        }

        bufferitemname(STR_VAR_1, ITEM_SAFARI_BALL)
        message(format("How many {STR_VAR_1}s would you like?"))
        waitmessage
        
        multichoice(20, 6, MULTI_MECHADOLL1_Q1, NO)

        switch(var(VAR_RESULT))
        {
            case 0:
                setvar(VAR_0x8000, 3)
                setvar(VAR_0x8001, 1000)
                call(RogueHub_EnterSafari1)

            case 1:
                setvar(VAR_0x8000, 6)
                setvar(VAR_0x8001, 1800)
                call(RogueHub_EnterSafari1)

            case 2:
                setvar(VAR_0x8000, 9)
                setvar(VAR_0x8001, 2500)
                call(RogueHub_EnterSafari1)

            case MULTI_B_PRESSED: 
                setvar(VAR_RESULT, NO)
        }
    }
    
    if(var(VAR_RESULT) == NO)
    {
        msgbox(format("Please Come again another time."))
        
        closemessage
        hidemoneybox
        applymovement(OBJ_EVENT_ID_PLAYER, Route121_SafariZoneEntrance_Movement_BackAwayFromCounter)
        waitmovement(0)
    }

    releaseall
}

script RogueHub_EnterSafari1
{
    checkmoney(VAR_0x8001)
    buffernumberstring(STR_VAR_1, VAR_0x8001)

    if(var(VAR_RESULT) == NO)
    {
        msgbox(format("That will cost ¥{STR_VAR_1}."))
        msgbox(format("Oh..\nYou don't appear to have enough money right now."))
    }
    else
    {
        msgbox(format("That will cost ¥{STR_VAR_1}.\nWould you like to enter?"), MSGBOX_YESNO)
        if(var(VAR_RESULT) == YES)
        {
            playse(SE_SHOP)
            removemoney(VAR_0x8001)
            updatemoneybox
            msgbox(format("Have fun!"))

            closemessage
            hidemoneybox
            applymovement(OBJ_EVENT_ID_PLAYER, Route121_SafariZoneEntrance_Movement_EnterSafariZone)
            waitmovement(0)
            special(EnterSafariMode)
            setvar(VAR_SAFARI_ZONE_STATE, 2)
            clearflag(FLAG_GOOD_LUCK_SAFARI_ZONE)
            warp(MAP_SAFARI_ZONE_SOUTH, 32, 33)
            waitstate
        }
    }
}

