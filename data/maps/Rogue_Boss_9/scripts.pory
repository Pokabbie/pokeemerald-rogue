const BOSS_NPC = 1

mapscripts Rogue_Boss_9_MapScripts
{
}

script Rogue_Battle_Boss9
{
    lockall()
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerToBattleFieldBoss9)
    waitmovement(0)

    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, CameraToBattleFieldBoss9)
    waitmovement(0)

    buffertrainername(STR_VAR_1, VAR_ROGUE_SPECIAL_ENCOUNTER_DATA)
    msgbox(format("{STR_VAR_1}: A new challenger!"))

    call(Rogue_SetDesiredWeather)

    applymovement(BOSS_NPC, BossToFightBoss9)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerToFightBoss9)
    waitmovement(0)
    
    // Manually kick off the encouter to control the music + avoid the trainer from changing directions
    playbgm(MUS_ENCOUNTER_ELITE_FOUR, FALSE)
    msgbox(format("Don't hold back now!"))
    trainerbattle(TRAINER_BATTLE_SINGLE_NO_INTRO_TEXT, VAR_ROGUE_SPECIAL_ENCOUNTER_DATA, 0, format("Well fought trainer!"))
    call(Rogue_BattleDefeated_Boss9)
}

script Rogue_BattleDefeated_Boss9
{
    call(Rogue_ClearWeather)
    
    message(format("{PLAYER} received a ELITE BADGE!"))
    waitmessage()
    call(Common_EventScript_PlayGymBadgeFanfare)
    msgbox(format("Good luck in your future battles!"))
    waitmessage()
    special(HealPlayerParty)
    special(RemoveCameraObject)
    warp(MAP_ROGUE_HUB_TRANSITION, 0)
}

movement PlayerToBattleFieldBoss9 {
    walk_up * 10
}

movement CameraToBattleFieldBoss9 {
    walk_up * 3
}

movement PlayerToFightBoss9 {
    walk_up
}

movement BossToFightBoss9 {
    walk_down
}